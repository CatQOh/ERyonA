;;;式中関数「LOOPRES(LOOP RESULT)」
;;;DO～LOOP構文の記述を簡略化するための関数
;;;引数で指定された数字とRESULTを参照し、一致していれば0を返す(つまりLOOPを抜ける)
;;;現在同時に指定できるのは10個まで
@LOOPRES, ARG:0 = __INT_MAX__, ARG:1 = __INT_MAX__, ARG:2 = __INT_MAX__, ARG:3 = __INT_MAX__, ARG:4 = __INT_MAX__, ARG:5 = __INT_MAX__, ARG:6 = __INT_MAX__, ARG:7 = __INT_MAX__, ARG:8 = __INT_MAX__, ARG:9 = __INT_MAX__
#FUNCTION
SIF MATCH(ARG, RESULT)
	RETURNF 0

RETURNF 1

;;;DO
;;;	INPUT
;;;LOOP LOOPRES(XX, XX, XX)
;;;を一行でやる関数 単純な選択肢などに使える
@INPUTF, 数値:0 = __INT_MAX__, 数値:1 = __INT_MAX__,数値:2 = __INT_MAX__,数値:3 = __INT_MAX__,数値:4 = __INT_MAX__,数値:5 = __INT_MAX__,数値:6 = __INT_MAX__,数値:7 = __INT_MAX__,数値:8 = __INT_MAX__,数値:9 = __INT_MAX__,
#DIM DYNAMIC 数値, 10
DO
	INPUT
LOOP LOOPRES(数値:0, 数値:1, 数値:2, 数値:3, 数値:4, 数値:5, 数値:6, 数値:7, 数値:8, 数値:9)

RETURN RESULT

;;;現在表示してる最後の行がDRAWLINE行かどうかをチェックし、そうでなければDRAWLINEを引く関数
@CHECKDRAWLINE
SIF HTML_TOPLAINTEXT(HTML_GETPRINTEDSTR(0)) != DRAWLINESTR
	DRAWLINE

;;;上記CHECKDRAWLINEのWAIT付き版
@CHECKDRAWLINEW
IF HTML_TOPLAINTEXT(HTML_GETPRINTEDSTR(0)) != DRAWLINESTR
	WAIT
	DRAWLINE
ENDIF

;;;eraBEMANIのMANUALPRINT タグを解析してPRINTする
@PRINTEXT, 行:0
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 検索位置
#DIMS DYNAMIC 行, 2
#DIM DYNAMIC 始点
#DIM DYNAMIC 文字数
#DIMS DYNAMIC 字体, 3
#DIMS DYNAMIC 本文

;タグを探す
始点 = STRFINDU(行, "(", 検索位置)
文字数 = STRFINDU(行, ")", 検索位置)-始点
;タグ付けされてれば処理実行
IF 始点 >= 0
	;タグ付けまでの文章を通常表示
	PRINTFORM %SUBSTRINGU(行, 0, 始点)%
	;処理、内容、本文でSPLITする
	SPLIT SUBSTRINGU(行, 始点+1, 文字数-1), ":", 字体
	本文 = %字体:2%
	;字体:0 = 特殊処理の種類 字体:1 = 特殊処理の内容 本文 = 本文
	;必要に応じて追加するとよい
	SELECTCASE 字体:0
		;フォント変更系
		CASE "FONT"
			SELECTCASE 字体:1
				;太字
				CASE "BOLD"
					FONTBOLD
					CALL PRINTEXT, 本文
					FONTREGULAR
				;打ち消し線
				CASE "STRIKE"
					FONTSTYLE 4
					CALL PRINTEXT, 本文
					FONTSTYLE 0
			ENDSELECT
		;色指定 現時点では色名のみで数値指定はできない
		;指定した色名が無効であればデフォルト色表示
		CASE "COLOR"
			IF COLOR_FROMNAME(字体:1) == -1
				CALL PRINTEXT, 本文
			ELSE
				SETCOLOR COLOR_FROMNAME(字体:1)
				CALL PRINTEXT, 本文
				RESETCOLOR
			ENDIF
		;その他
		CASE "OTHER"
			SELECTCASE 字体:1
				;反転タ ALIGHNMENT命令の仕様上、タグ重複使用は不可能
				CASE "REVERSE"
					行:1 =
					FOR LCOUNT, STRLENSU(本文), -1, -1
						SELECTCASE CHARATU(本文, LCOUNT)
							CASE "【"
								行:1 += "】"
							CASE "】"
								行:1 += "【"
							CASE "「"
								行:1 += "」"
							CASE "」"
								行:1 += "「"
							CASE "["
								行:1 += "]"
							CASE "]"
								行:1 += "["
							CASE "（"
								行:1 += "）"
							CASE "）"
								行:1 += "（"
							CASEELSE
								行:1 += CHARATU(本文, LCOUNT)
						ENDSELECT
					NEXT
					ALIGNMENT RIGHT
					PRINTFORML %行:1%
					ALIGNMENT LEFT
				;WAIT このタグを使用すると改行されるので、文末までタグに含めないと表示がおかしくなる
				CASE "WAIT"
					CALL PRINTEXT, 本文
					WAIT
			ENDSELECT
	ENDSELECT
	;次の始動位置をタグ終了後の次の文字に設定
	検索位置 = STRFINDU(行, 本文)+STRLENSU(本文)
	IF 検索位置 < STRLENSU(行)
		;残りの文章が閉じカッコならループ終了
		IF 検索位置+1 == STRLENSU(行) && CHARATU(行, 検索位置) == ")"
			RETURN
		;残りの文章が閉じカッコ以外にもあれば再帰処理
		ELSE
			;閉じカッコの分+1
			CALL PRINTEXT, SUBSTRINGU(行, 検索位置+1)
			RETURN
		ENDIF
	ENDIF
;タグが無ければ通常表示
ELSE
	PRINTFORM %行%
	RETURN
ENDIF

;;;PRINTEXTを平文にしてSUBSTRINGする タグ入れ子には非対応
@TAGSTRLENS, 行
#FUNCTION

#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 検索位置
#DIMS DYNAMIC 行, 2
#DIM DYNAMIC 始点
#DIM DYNAMIC 文字数
#DIMS DYNAMIC 字体, 3
#DIMS DYNAMIC 本文
#DIMS DYNAMIC 平文

;タグを探す
DO
	始点 = STRFINDU(行, "(", 検索位置)
	文字数 = STRFINDU(行, ")", 検索位置)-始点
	;タグ付けされてれば処理実行
	IF 始点 >= 0
		;タグ付けまでの文章
		平文 += SUBSTRINGU(行, 検索位置, 始点-検索位置)
		;処理、内容、本文でSPLITする
		SPLIT SUBSTRINGU(行, 始点+1, 文字数-1), ":", 字体
		本文 = %字体:2%
		平文 += 字体:2
		DEBUGPRINTFORML %平文%
		;次の始動位置をタグ終了後の次の文字に設定
		検索位置 = STRFINDU(行, 本文)+STRLENSU(本文)
		IF 検索位置 < STRLENSU(行)
			;残りの文章が閉じカッコならループ終了
			IF 検索位置+1 == STRLENSU(行) && CHARATU(行, 検索位置) == ")"
				RETURNF STRLENS(平文)
			;残りの文章が閉じカッコ以外にもあればループ
			ELSE
				;閉じカッコの分+1
				検索位置++
				CONTINUE
			ENDIF
		ENDIF
	ELSE
		平文 += SUBSTRINGU(行, 検索位置, STRLENSU(行))
		DEBUGPRINTFORML %平文%
		RETURNF STRLENS(平文)
	ENDIF
LOOP 1

@COLUMNLINE, LINE, 文字数
#DIM DYNAMIC LINE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 文字数

CALL PRINTEXT, カラム行:LINE

FOR LCOUNT, 0, 文字数-TAGSTRLENS(カラム行:LINE)
	PRINT  
NEXT

;;;ループやRESTART時に同じ物を表示しないようにCLEARLINEする関数
@LOOP_CLEARLINE
SIF VARLINE > 0
	CLEARLINE LINECOUNT-VARLINE
VARLINE = LINECOUNT

;;;キャラの最大HP
@MAXHP, 対象
#FUNCTION
#DIM DYNAMIC 対象

RETURNF 10+ステータス:対象:レベル+(ステータス:対象:耐久*2)

;;;キャラの最大MP
@MAXMP, 対象
#FUNCTION
#DIM DYNAMIC 対象

RETURNF 5+(ステータス:対象:レベル/2)+ステータス:対象:知性

;;;キャラの最大VP
@MAXVP, 対象
#FUNCTION
#DIM DYNAMIC 対象

RETURNF 5+(ステータス:対象:レベル/2)

;;;上記SPACESとSTRLENSを組み合わせたもの
@SPACESLENS, 文字列
#FUNCTIONS
#DIMS DYNAMIC 文字列
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC SPACE

FOR LCOUNT, 0, STRLENS(文字列)
	SPACE += " "
NEXT

RETURNF SPACE

;;;条件がtrueだった場合は文字列を、falseなら文字数分の空白を返す
@RETURNSPACE, 条件, 文字列
#FUNCTIONS
#DIM DYNAMIC 条件
#DIMS DYNAMIC 文字列
IF 条件
	RETURNF 文字列
ELSE
	RETURNF SPACESLENS(文字列)
ENDIF

;;;指定したアイテムIDがどの部隊のものでもない（拠点で扱えるフリーなものか）を判別する
@拠点アイテム, ITEMID
#FUNCTION
#DIM DYNAMIC ITEMID

SIF DT_CELL_GETS("アイテム", ITEMID, "名前") == ""
	RETURNF 0

RETURNF (DT_CELL_ISNULL("アイテム", ITEMID, "所有部隊") || DT_CELL_GET("アイテム", ITEMID, "所持部隊") == 0) && (DT_CELL_ISNULL("アイテム", ITEMID, "装備中") || DT_CELL_GET("アイテム", ITEMID, "装備中") == 0)

@拠点アイテム総数, ARG
#FUNCTION
#DIM DYNAMIC ITEMCOUNT
#DIM DYNAMIC LCOUNT

FOR LCOUNT, 0, DT_ROW_LENGTH("アイテム")
	SIF (DT_CELL_ISNULL("アイテム", LCOUNT, "所有部隊") || DT_CELL_GET("アイテム", LCOUNT, "所持部隊") == 0) && (DT_CELL_ISNULL("アイテム", LCOUNT, "装備中") || DT_CELL_GET("アイテム", LCOUNT, "装備中") == 0)
		ITEMCOUNT++
NEXT

RETURNF ITEMCOUNT


@アイテム名, ITEMID
#FUNCTIONS
#DIM DYNAMIC ITEMID

RETURNF DT_CELL_GETS("アイテム", ITEMID, "名前")

@レア度, ITEMID
#FUNCTION
#DIM DYNAMIC ITEMID

RETURNF DT_CELL_GET("アイテム", ITEMID, "レア度")

@装備中, ITEMID
#FUNCTION
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "装備中")
	RETURNF 0
RETURNF DT_CELL_GET("アイテム", ITEMID, "装備中")

@装備手, ITEMID
#FUNCTIONS
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "装備手")
	RETURNF ""
RETURNF DT_CELL_GETS("アイテム", ITEMID, "装備手")

@所有部隊, ITEMID
#FUNCTION
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "所有部隊")
	RETURNF 0
RETURNF DT_CELL_GET("アイテム", ITEMID, "所有部隊")

@耐久値, ITEMID
#FUNCTION
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "耐久値")
	RETURNF 0
RETURNF DT_CELL_GET("アイテム", ITEMID, "耐久値")

@分類, ITEMID
#FUNCTIONS
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "分類")
	RETURNF ""
RETURNF DT_CELL_GETS("アイテム", ITEMID, "分類")

@攻撃力, ITEMID
#FUNCTION
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "攻撃力")
	RETURNF 0
RETURNF DT_CELL_GET("アイテム", ITEMID, "攻撃力")

@防御力, ITEMID
#FUNCTION
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "防御力")
	RETURNF 0
RETURNF DT_CELL_GET("アイテム", ITEMID, "防御力")

@命中率, ITEMID
#FUNCTION
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "命中率")
	RETURNF 0
RETURNF DT_CELL_GET("アイテム", ITEMID, "命中率")

@特殊効果, ITEMID
#FUNCTIONS
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "特殊効果")
	RETURNF ""
RETURNF DT_CELL_GETS("アイテム", ITEMID, "特殊効果")

@重量, ITEMID
#FUNCTION
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "重量")
	RETURNF 0
RETURNF DT_CELL_GET("アイテム", ITEMID, "重量")

@要求ステータス, ITEMID, 要求ステータス
#FUNCTION
#DIM DYNAMIC ITEMID
#DIMS DYNAMIC 要求ステータス

SELECTCASE 要求ステータス
	CASE "必要筋力", "筋力"
		SIF DT_CELL_ISNULL("アイテム", ITEMID, "必要筋力")
			RETURNF 0
		RETURNF DT_CELL_GET("アイテム", ITEMID, "必要筋力")
	CASE "必要耐久", "耐久"
		SIF DT_CELL_ISNULL("アイテム", ITEMID, "必要耐久")
			RETURNF 0
		RETURNF DT_CELL_GET("アイテム", ITEMID, "必要耐久")
	CASE "必要知性", "知性"
		SIF DT_CELL_ISNULL("アイテム", ITEMID, "必要知性")
			RETURNF 0
		RETURNF DT_CELL_GET("アイテム", ITEMID, "必要知性")
	CASE "必要器用", "器用"
		SIF DT_CELL_ISNULL("アイテム", ITEMID, "必要器用")
			RETURNF 0
		RETURNF DT_CELL_GET("アイテム", ITEMID, "必要器用")
	CASE "必要感覚", "感覚"
		SIF DT_CELL_ISNULL("アイテム", ITEMID, "必要感覚")
			RETURNF 0
		RETURNF DT_CELL_GET("アイテム", ITEMID, "必要感覚")
	CASE "必要運勢", "運勢"
		SIF DT_CELL_ISNULL("アイテム", ITEMID, "必要運勢")
			RETURNF 0
		RETURNF DT_CELL_GET("アイテム", ITEMID, "必要運勢")
ENDSELECT

@説明, ITEMID
#FUNCTIONS
#DIM DYNAMIC ITEMID

SIF DT_CELL_ISNULL("アイテム", ITEMID, "説明")
	RETURNF ""
RETURNF DT_CELL_GETS("アイテム", ITEMID, "説明")

@アイテム削除, ITEMID
#DIM DYNAMIC ITEMID

DT_ROW_REMOVE "アイテム", DT_CELL_GET("アイテム", ITEMID, "id")
