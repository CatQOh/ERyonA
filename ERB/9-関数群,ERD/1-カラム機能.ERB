@カラム生成, カラム, カラムメニュー:0, カラムメニュー:1, カラムメニュー:2, カラムメニュー:3, カラムメニュー:4, カラムメニュー:5, カラムメニュー:6, カラムメニュー:7, カラムメニュー:8, カラムメニュー:9
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LINE
;MAX_LINEは可変にしてみる
;#DIM CONST MAX_LINE = 30
#DIM 表示カラム
#DIM DYNAMIC 文字数計算用
#DIM DYNAMIC 行文字数 = STRLENSU(GETLINESTR("─"))
;#DIM DYNAMIC 選択カラム = 0
#DIM DYNAMIC 平文文字数
#DIMS DYNAMIC カラムメニュー, 10
#DIM DYNAMIC カラム数
#DIMS カラム
REDRAW 0
CALL LOOP_CLEARLINE

ARRAYCOPY "カラムメニュー", "カラム名"

FOR LCOUNT, 0, VARSIZE("カラムメニュー")
	IF カラムメニュー:LCOUNT == ""
		カラム数 = LCOUNT
		BREAK
	ENDIF
NEXT

CALL CHECKDRAWLINE

FOR LCOUNT, 0, VARSIZE("カラムメニュー")
	文字数計算用 = MAX(STRLENSU(カラムメニュー:LCOUNT)+3, 文字数計算用)
NEXT

;罫線3つ分引いてSTRLENSU→STRLENS変換で2倍
平文文字数 = (行文字数-文字数計算用-3)*2

VARSET カラム行

FOR LINE, 0, MAX_LINE
	FOR LCOUNT, 0, 行文字数
		SELECTCASE LINE
			CASE 0
				SELECTCASE LCOUNT
					CASE 0
						PRINT ┌
					CASE 行文字数-1
						PRINT ┐
					CASEELSE
						IF LCOUNT == 文字数計算用+1 && 選択カラム != 0
							PRINT ┬
						ELSE
							PRINT ─
						ENDIF
				ENDSELECT
			CASE MAX_LINE-1
				SELECTCASE LCOUNT
					CASE IS < 文字数計算用+1
						PRINT 　
					CASE 文字数計算用+1
						PRINT └
					CASE 行文字数-1
						PRINT ┘
					CASEELSE
						PRINT ─
				ENDSELECT
			CASEELSE
				IF LINE/2 < カラム数
					IF LINE%2
						SELECTCASE LCOUNT
							CASE 0
								PRINTFORM │[{LINE/2+100}] %カラムメニュー:(LINE/2), (文字数計算用-3)*2, LEFT%
							CASE 行文字数-1
								PRINT │
							CASE IS > 文字数計算用
								IF LCOUNT == 文字数計算用+1
									IF 選択カラム != LINE/2
										PRINTPLAIN │
									ELSE
										PRINTPLAIN 　
									ENDIF
								ELSEIF LCOUNT == 文字数計算用+2
									TRYCCALLFORM %カラム%_%カラムメニュー:選択カラム%
										CALL COLUMNLINE, LINE-1, 平文文字数
									CATCH
										PRINTFORM %" ", 平文文字数, LEFT%
									ENDCATCH
								ENDIF
						ENDSELECT
					ELSE
						SELECTCASE LCOUNT
							CASE 0
								;IF 選択カラム < LINE/2
									PRINT ├
								;ELSE
								;	PRINT ┌
								;ENDIF
							CASE IS <= 文字数計算用
								PRINT ─
							CASE 文字数計算用+1
								IF 選択カラム < LINE/2
									IF LINE == (選択カラム*2)+2
										PRINTPLAIN ┐
									ELSE
										PRINTPLAIN ┤
									ENDIF
								ELSE
									IF LINE == (選択カラム*2)
										PRINTPLAIN ┘
									ELSE
										PRINTPLAIN ┤
									ENDIF
								ENDIF
							CASE 行文字数-1
								PRINTPLAIN │
							CASE 文字数計算用+2
								TRYCCALLFORM %カラム%_%カラムメニュー:選択カラム%
										CALL COLUMNLINE, LINE-1, 平文文字数
								CATCH
									PRINTFORM %" ", 平文文字数, LEFT%
								ENDCATCH
						ENDSELECT
					ENDIF
				ELSEIF LINE/2 == カラム数 && LINE%2 == 0
					SELECTCASE LCOUNT
						CASE 0
							PRINT └
						CASE IS <= 文字数計算用
							PRINT ─
						CASE 文字数計算用+1
							IF LINE == (選択カラム*2)+2
								PRINTPLAIN ┐
							ELSE
								PRINTPLAIN ┤
							ENDIF
						CASE 行文字数-1
							PRINTPLAIN │
						CASE 文字数計算用+2
							TRYCCALLFORM %カラム%_%カラムメニュー:選択カラム%
								CALL COLUMNLINE, LINE-1, 平文文字数
							CATCH
								PRINTFORM %" ", 平文文字数, LEFT%
							ENDCATCH
					ENDSELECT
				ELSE
					SELECTCASE LCOUNT
						CASE IS < 文字数計算用+1
							PRINT 　
						CASE 文字数計算用+1
							PRINTPLAIN │
						CASE 行文字数-1
							PRINTPLAIN │
						CASE 文字数計算用+2
							TRYCCALLFORM %カラム%_%カラムメニュー:選択カラム%
								CALL COLUMNLINE, LINE-1, 平文文字数
							CATCH
								PRINTFORM %" ", 平文文字数, LEFT%
							ENDCATCH
					ENDSELECT
				ENDIF
		ENDSELECT
	NEXT
	PRINTL 
NEXT

INPUT

SELECTCASE RESULT
	CASE 100 TO 110
		選択カラム = RESULT-100
		CRESULT:1 = -1
		RESTART
	CASEELSE
		CRESULT:0 = 選択カラム
		CRESULT:1 = RESULT
		REDRAW 1
		RETURN
ENDSELECT
