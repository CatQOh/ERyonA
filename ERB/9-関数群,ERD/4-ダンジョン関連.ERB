@ダンジョンDT作成
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
{
#DIMS DYNAMIC DTNAME =
"ダンジョン",
}
{
#DIMS DYNAMIC COLUMNNAME = 
"名前",
"レベル",
"探索部隊",
}

FOR LCOUNT, 0, VARSIZE("DTNAME")
	DT_CREATE DTNAME:LCOUNT
	SIF !RESULT
		CONTINUE
	FOR LCOUNT2, 0, VARSIZE("COLUMNNAME")
		SELECTCASE COLUMNNAME:LCOUNT2
			CASE "名前"
				DT_COLUMN_ADD DTNAME:LCOUNT, COLUMNNAME:LCOUNT2, "string"
			CASEELSE
				DT_COLUMN_ADD DTNAME:LCOUNT, COLUMNNAME:LCOUNT2, "int8"
		ENDSELECT
	NEXT
NEXT

DT_COLUMN_OPTIONS "ダンジョン", "探索部隊", DEFAULT, -1

@ダンジョン生成
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 不足分
#DIM DYNAMIC 乱数
#DIMS DYNAMIC ダンジョン名

不足分 = 10-DT_ROW_LENGTH("ダンジョン")

SIF 不足分 == 0
	RETURN

DUMPRAND
乱数 = RANDDATA
RANDOMIZE シード値:ダンジョン

;ダンジョン名生成は後で作る
ダンジョン名 = ダンジョン

FOR LCOUNT, 0, 不足分
	DT_ROW_ADD "ダンジョン", "名前", ダンジョン名, "レベル", RAND:DAY+1
NEXT

DUMPRAND
シード値:ダンジョン = RANDDATA
RANDOMIZE 乱数

@ダンジョン名, DID
#FUNCTIONS
#DIM DYNAMIC DID

RETURNF DT_CELL_GETS("ダンジョン", DID, "名前")

@ダンジョンレベル, DID
#FUNCTION
#DIM DYNAMIC DID

RETURNF DT_CELL_GET("ダンジョン", DID, "レベル")

@ダンジョン探索部隊, DID
#FUNCTION
#DIM DYNAMIC DID

RETURNF DT_CELL_GET("ダンジョン", DID, "探索部隊")
