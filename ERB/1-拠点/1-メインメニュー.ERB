@メインメニュー
#DIM NOWCOLUMN
SIF 加入候補 == -1
	CALL 加入候補生成

DO
	CALL カラム生成, "メインメニュー", "ダンジョン探索", "部隊編成", "人員の補充", "アイテム", "セーブ＆ロード"
	NOWCOLUMN = CRESULT:0
	SELECTCASE カラム名:(CRESULT:0)
		CASE "ダンジョン探索"
		CASE "部隊編成"
			SELECTCASE CRESULT:1
				CASE 1, 10
					CALL 部隊編成, CRESULT:1
			ENDSELECT
		CASE "人員の補充"
			SELECTCASE CRESULT:1
				CASE 10 TO 14
					ステータス:(加入候補:(CRESULT:1-10)):未加入 = 0
					加入済み:(CRESULT:1-10) = 1
			ENDSELECT
		CASE "アイテム"
			SELECTCASE CRESULT:1
				CASE 0 TO 99
					選択アイテム = CRESULT:1
				CASE 201
					SIF アイテムページ > 0
						アイテムページ--
				CASE 209
					SIF アイテムページ < 9
						アイテムページ++
				CASE 210
					IF レア度(選択アイテム)
						売却アイテム名 = %アイテム名(選択アイテム)%
						売却額 = レア度(選択アイテム)
						資源 += 売却額
						CALL アイテム削除(選択アイテム)
					ENDIF
			ENDSELECT
		CASE "セーブ＆ロード"
			SELECTCASE CRESULT:1
				CASE 0 TO 9
					IF CHKDATA(CRESULT:1) == 0
						LOADDATA CRESULT:1
					ENDIF
				CASE 10
					SAVEDATA SAVEID, @"{SAVEID}"
			ENDSELECT
	ENDSELECT
	CRESULT:0 = NOWCOLUMN
LOOP 1

@メインメニュー_ダンジョン探索
#DIM DYNAMIC LINE

カラム行:(LINE++) = [0] - ダンジョン探索です
カラム行:(LINE++) = [1] - 1です


@メインメニュー_部隊編成
#DIM DYNAMIC LCOUNT

FOR LCOUNT, 1, 11
	カラム行:LCOUNT = [{LCOUNT}] 部隊{LCOUNT}
NEXT

@メインメニュー_人員の補充
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 対象
#DIM DYNAMIC LINE

FOR LCOUNT, 0, 5
	SIF 加入済み:LCOUNT
		CONTINUE
	対象 = 加入候補:LCOUNT
	カラム行:(LINE++) = 名前:%NAME:対象% レベル:{ステータス:対象:レベル} 職業:%CSTR:対象:職業% (COLOR:lime:HP：{ステータス:対象:HP}/{MAXHP(対象)}) (COLOR:deepskyblue:MP：{ステータス:対象:MP}/{MAXMP(対象)}) (COLOR:yellow:VP：{ステータス:対象:VP}/{MAXVP(対象)}) [{LCOUNT}] - 雇う\@ ステータス:対象:レベル > 1 ? （資源{ステータス:対象:レベル}個必要）#\@
	FOR LCOUNT2, 0, 6, 2
		カラム行:(LINE++) = %ERDNAME(ステータス, LCOUNT2)%%"■"*ステータス:対象:LCOUNT2%%"□"*(20-ステータス:対象:LCOUNT2)% {ステータス:対象:LCOUNT2} %ERDNAME(ステータス, (LCOUNT2+1))%%"■"*ステータス:対象:(LCOUNT2+1)%%"□"*(20-ステータス:対象:(LCOUNT2+1))% {ステータス:対象:(LCOUNT2+1)}
	NEXT
	カラム行:(LINE++) = 
NEXT
SELECTCASE CRESULT:1
	CASE 0 TO 4
		カラム行:(LINE++) = %NAME:(加入候補:(CRESULT:1))%を雇いますか？
		カラム行:(LINE++) = [{CRESULT:1+10}] はい [20] いいえ
	CASE 10 TO 14
		IF ステータス:対象:レベル > 1 && 資源 < ステータス:対象:レベル
			カラム行:(LINE++) = 資源が足りません
			RETURN
		ENDIF
		カラム行:(LINE++) = %NAME:(加入候補:(CRESULT:1-10))%を雇った
ENDSELECT

@メインメニュー_アイテム
#DIM DYNAMIC LINE
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 分類
#DIMS DYNAMIC 装備条件
#DIM DYNAMIC 要求値
#DIMS DYNAMIC 文字色
{
#DIMS DYNAMIC 要求項目 =
"必要筋力",
"必要耐久",
"必要知性",
"必要器用",
"必要感覚",
"必要運勢",
}

カラム行:(LINE++) = □資源:{資源}

FOR LCOUNT, アイテムページ*10, アイテムページ*10+10
	IF !拠点アイテム(LCOUNT)
		カラム行:(LINE++) = 
	ELSE
		SELECTCASE レア度(LCOUNT)
			CASE 1
				文字色 = silver
			CASE 2
				文字色 = lime
			CASE 3
				文字色 = deepskyblue
			CASE 4
				文字色 = purple
			CASE IS >= 5
				文字色 = gold
		ENDSELECT
		カラム行:(LINE++) = (COLOR:%文字色%:[{LCOUNT}] %アイテム名(LCOUNT)%（レア度：{レア度(LCOUNT)}）)
	ENDIF
NEXT
カラム行:(LINE++) = %RETURNSPACE(アイテムページ > 0, "[201] 前ページ")% %RETURNSPACE(DT_ROW_LENGTH("アイテム") > LCOUNT && 拠点アイテム(LCOUNT+1), "[209] 次ページ")%
IF CRESULT:1 == 210
	カラム行:(LINE++) = 　%売却アイテム名%を売却しました（資源{売却額}個分）
ELSEIF CRESULT:1 >= 0 && アイテム名(CRESULT:1) != ""
	FOR LCOUNT, 0, VARSIZE("要求項目")
		要求値 = 要求ステータス(CRESULT:1, 要求項目:LCOUNT)
		SIF 要求値
			装備条件 += @"%要求項目:LCOUNT%:{要求値} "
	NEXT
	分類 = %DT_CELL_GETS("アイテム", CRESULT:1, "分類")%
	カラム行:(LINE++) = □%DT_CELL_GETS("アイテム", CRESULT:1, "名前")%（ID:{CRESULT:1}）
	カラム行:(LINE++) = 　レア度:{レア度(CRESULT:1)} \@ 分類 == "消耗品" ? 残り使用回数 # 耐久値 \@:{耐久値(CRESULT:1)} 分類:%分類%
	SIF 装備条件 != ""
		カラム行:(LINE++) = 　%装備条件%
	カラム行:(LINE++) = 　\@ 攻撃力(CRESULT:1) ? 攻撃力:{攻撃力(CRESULT:1)}%" "% #\@\@ 防御力(CRESULT:1) ? 防御力:{防御力(CRESULT:1)}%" "% #\@\@ 命中率(CRESULT:1) ? 命中率:{命中率(CRESULT:1)}%" "% #\@\@ 特殊効果(CRESULT:1) != "" ? 特殊効果:%特殊効果(CRESULT:1)%%" "% #\@\@ 重量(CRESULT:1) ? 重量:{重量(CRESULT:1)} #\@
	カラム行:(LINE++) = 　%説明(CRESULT:1)%
	カラム行:(LINE++) = 　[210] 売却する（資源{レア度(CRESULT:1)}個分）
ENDIF

@メインメニュー_セーブ＆ロード
#DIM DYNAMIC LINE
#DIM DYNAMIC LCOUNT

カラム行:(LINE++) = □セーブ
カラム行:(LINE++) = [10] セーブする（セーブデータID:{SAVEID}）
カラム行:(LINE++) = 
カラム行:(LINE++) = □ロード
FOR LCOUNT, 0, 10
	SELECTCASE CHKDATA(LCOUNT)
		CASE 0
			カラム行:(LINE++) = [{LCOUNT}] %RESULTS%
		CASE 1
			カラム行:(LINE++) = (COLOR:gray:[{LCOUNT}] ----)
			RESETCOLOR
		CASE 2 TO 4
			カラム行:(LINE++) = (COLOR:gray:[{LCOUNT}] 無効なセーブデータ)
	ENDSELECT
NEXT
カラム行:(LINE++) = 
SIF CRESULT:1 == 10
	カラム行:(LINE++) = セーブしました

@加入候補生成
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 対象

RANDOMIZE シード値:キャラクター
FOR LCOUNT, 0, 5
	ADDVOIDCHARA
	加入候補:LCOUNT = CHARANUM-1
	対象 = 加入候補:LCOUNT
	NAME:対象 = %ランダム命名(RAND(1, 4))%
	;3人目までが資源が必要 4～5人目は志願兵
	SELECTCASE LCOUNT
		CASE 0 TO 2
			ステータス:対象:レベル = RAND(6, 11)
		CASE 3, 4
			ステータス:対象:レベル = 1
	ENDSELECT
	CSTR:対象:職業 = %職業一覧:(RAND:VARSIZE("職業一覧"))%
	CALL 職業初期値, CSTR:対象:職業, 対象
	FOR LCOUNT2, 1, ステータス:対象:レベル 
		ステータス:対象:(RAND:6)++
	NEXT
	ステータス:対象:HP = MAXHP(対象)
	ステータス:対象:MP = MAXMP(対象)
	ステータス:対象:VP = MAXVP(対象)
	;ステータスの設定 保留
	ステータス:対象:未加入 = 1
NEXT
