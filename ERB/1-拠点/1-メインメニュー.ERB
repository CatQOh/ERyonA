@メインメニュー
#DIM NOWCOLUMN
#DIM DYNAMIC LCOUNT
SIF !加入候補生成済み
	CALL 加入候補生成

CALL ダンジョン生成

CALL カラム生成, "メインメニュー", "ダンジョン探索", "部隊編成", "キャラ一覧", "人員の補充", "アイテム", "セーブ＆ロード"

RESTART

@メインメニュー_ダンジョン探索
#DIM DYNAMIC LCOUNT

IF 選択ダンジョン != -1
	CALL COLUMNPRINTL, 0, "□探索する部隊を選んでください"
	FOR LCOUNT, 1, 部隊数+1
		SIF 部隊人数(LCOUNT) && 部隊目標:LCOUNT == -1
			CALL COLUMNPRINTL, 0, @"　[{LCOUNT}] 部隊{LCOUNT}"
	NEXT
	CALL COLUMNPRINTL, 0, "　[-1] 戻る"
ELSE
	CALL COLUMNPRINTL, 0, @"□{DAY}日目 探索するダンジョンを選択してください"

	FOR LCOUNT, 0, 10
		CALL COLUMNPRINT, 0, @"　[{LCOUNT}]%ダンジョン名(LCOUNT)% レベル:{ダンジョンレベル(LCOUNT)} "
		IF ダンジョン探索部隊(LCOUNT) != -1
			IF 部隊人数(ダンジョン探索部隊(LCOUNT)) == 0
				DT_CELL_SET "ダンジョン", LCOUNT, "ダンジョン探索部隊", -1
			ELSE
				CALL COLUMNPRINT, 0, @"部隊{ダンジョン探索部隊(LCOUNT)}が探索予定 再選択で中止"
			ENDIF
		ENDIF
		CALL COLUMNPRINTL, 0, ""
	NEXT
	CALL COLUMNPRINTL, 0, ""
	IF 出撃部隊無し
		CALL COLUMNPRINTL, 0, "　出撃する部隊が居ません。本当に1日を終了してよろしいですか？"
		CALL COLUMNPRINTL, 0, "　[21] はい [22] いいえ"
	ELSE
		CALL COLUMNPRINTL, 0, "　[20] この編成で探索する(1日を終了する)"
	ENDIF
ENDIF

@メインメニュー_ダンジョン探索_INPUT
#DIM DYNAMIC LCOUNT

IF 選択ダンジョン >= 0
	IF INRANGE(CRESULT:1, 1, 10)
		DT_CELL_SET "ダンジョン", 選択ダンジョン, "探索部隊", CRESULT:1
		部隊目標:(CRESULT:1) = 選択ダンジョン
	ENDIF
	選択ダンジョン = -1
ELSE
	SELECTCASE CRESULT:1
		CASE 0 TO 9
			選択ダンジョン = CRESULT:1
			IF ダンジョン探索部隊(選択ダンジョン) >= 0
				部隊目標:ダンジョン探索部隊(選択ダンジョン) = -1
				DT_CELL_SET "ダンジョン", 選択ダンジョン, "探索部隊", -1
				CRESULT:1 = -1
				選択ダンジョン = -1
			ENDIF
		CASE 20
			FOR LCOUNT, 1, 11
				SIF 部隊目標:LCOUNT != -1
					BREAK
				SIF LCOUNT == 10
					出撃部隊無し = 1
			NEXT
			IF !出撃部隊無し
				CALL ダンジョン探索
				CALL 日付変更
			ENDIF
		CASE 21
			IF 出撃部隊無し
				CALL 日付変更
				出撃部隊無し = 0
			ENDIF
		CASE 22
			出撃部隊無し = 0
			CRESULT:1 = -1
	ENDSELECT
ENDIF


@メインメニュー_部隊編成
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 対象
#DIM DYNAMIC ID
#DIM ページ
#DIM DYNAMIC 表示数
#DIM 次キャラあり
ID = 部隊編成

SELECTCASE CRESULT:1
	CASE -1
		SIF ページ > 0
			ページ--
	CASE -9
		SIF 次キャラあり
			ページ++
ENDSELECT

IF 部隊編成
	;FOR LCOUNT, 0, CHARANUM
	;	SIF ステータス:所属部隊 == ID
	;		ARRAYSHIFT 隊員, 1, LCOUNT
	;NEXT
	IF 部隊スロット != -1
		CALL COLUMNPRINTL, 0, @"□部隊{ID}に誰を配属しますか？"
		表示数 = 0
		次キャラあり = 0
		FOR LCOUNT, 0, CHARANUM
			SIF ページ*5 > LCOUNT
				CONTINUE
			SIF ステータス:LCOUNT:所属部隊
				CONTINUE
			SIF ステータス:LCOUNT:VP == 0
				CONTINUE
			SIF ステータス:LCOUNT:未加入
				CONTINUE
			IF 表示数 >= 5
				次キャラあり = 1
				BREAK
			ENDIF
			CALL ステータス表示, LCOUNT, @"[{LCOUNT}]"
			CALL COLUMNPRINTL, 0, ""
			表示数++
		NEXT
		FOR LCOUNT, 0, 5-表示数
			CALL COLUMNPRINTL, 0, ""
			CALL COLUMNPRINTL, 0, ""
			CALL COLUMNPRINTL, 0, ""
			CALL COLUMNPRINTL, 0, ""
		NEXT
		CALL COLUMNPRINTL, 0, @"%RETURNSPACE(ページ, "[-1] 前ページ")% [0] 戻る %RETURNSPACE(次キャラあり, "[-9] 次ページ")%"
	ELSE
		CALL COLUMNPRINTL, 0, @"□部隊{ID}の編成"
		FOR LCOUNT, 0, 3
			IF !部隊メンバー:ID:LCOUNT
				CALL COLUMNPRINTL, 0, @"　空き [{LCOUNT}] - 配属"
				CONTINUE
			ENDIF
			対象 = GETCHARA(部隊メンバー:ID:LCOUNT)

			CALL ステータス表示, 対象, @"[{LCOUNT}] - 変更する"
			CALL COLUMNPRINTL, 0, ""
		NEXT
		CALL COLUMNPRINTL, 0, "　[10] 戻る"
	ENDIF
ELSE
	CALL COLUMNPRINTL, 0, @"□部隊編成 \@ 部隊数 < 10 ? [0] 部隊数を増やす(資源{部隊数*100}個必要) #\@"
	FOR LCOUNT, 1, 部隊数+1
		CALL COLUMNPRINTL, 0, @"　[{LCOUNT}] 部隊{LCOUNT}"
	NEXT
ENDIF

@メインメニュー_部隊編成_INPUT
#DIM DYNAMIC LCOUNT

IF 部隊編成
	IF 部隊スロット != -1
		IF CRESULT:1 == 0
			部隊スロット = -1
		ELSE
			SIF 部隊メンバー:部隊編成:部隊スロット
				ステータス:GETCHARA(部隊メンバー:部隊編成:部隊スロット):所属部隊 = 0
			部隊メンバー:部隊編成:部隊スロット = NO:(CRESULT:1)
			ステータス:(CRESULT:1):所属部隊 = 部隊編成
			CRESULT:1 = 0
			部隊スロット = -1
		ENDIF
	ELSE
		IF CRESULT:1 == 10
			部隊編成 = 0
		ELSE
			部隊スロット = CRESULT:1
		ENDIF
	ENDIF
ELSE
	SELECTCASE CRESULT:1
		CASE 1 TO 部隊数
			部隊編成 = CRESULT:1
			;CALL 部隊編成, CRESULT:1
		CASE 0
			IF 資源 >= 部隊数*100
				資源 -= 部隊数*100
				部隊数++
			ENDIF
	ENDSELECT
ENDIF

@メインメニュー_キャラ一覧
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 表示数
#DIM ページ
#DIM キャラ詳細表示
#DIM 次キャラあり

IF キャラ詳細表示
	SELECTCASE CRESULT:1
		CASE -1
			SIF キャラ詳細表示 > 1
				キャラ詳細表示--
		CASE -9
			SIF キャラ詳細表示 < CHARANUM-1
				キャラ詳細表示++
		CASE 0
			キャラ詳細表示 = 0
	ENDSELECT
ELSE
	SELECTCASE CRESULT:1
		CASE -1
			SIF ページ > 0
				ページ--
		CASE -9
			SIF 次キャラあり
				ページ++
		CASEELSE
			IF INRANGE(CRESULT:1, 1, CHARANUM-1)
				SIF !ステータス:(CRESULT:1):未加入
					キャラ詳細表示 = CRESULT:1
			ENDIF
	ENDSELECT
ENDIF

IF キャラ詳細表示
	CALL COLUMNPRINTL, 0, "キャラ表示"
	CALL COLUMNPRINTL, 0, "[0] - 戻る"
ELSE
	CALL COLUMNPRINTL, 0, @"□キャラ一覧 生存者数/総数:{生存者数()}/{キャラ総数()}"
	表示数 = 0
	次キャラあり = 0
	FOR LCOUNT, 0, CHARANUM
		SIF LCOUNT == 0
			CONTINUE
		SIF ページ*5 > LCOUNT
			CONTINUE
		SIF ステータス:LCOUNT:未加入
			CONTINUE
		IF 表示数 >= 5
			次キャラあり = 1
			BREAK
		ENDIF
		CALL ステータス表示, LCOUNT, @"[{LCOUNT}] - 詳細"
		CALL COLUMNPRINTL, 0, ""
		表示数++
	NEXT
	FOR LCOUNT, 0, 5-表示数
		CALL COLUMNPRINTL, 0, ""
		CALL COLUMNPRINTL, 0, ""
		CALL COLUMNPRINTL, 0, ""
		CALL COLUMNPRINTL, 0, ""
	NEXT
	CALL COLUMNPRINTL, 0, @"%RETURNSPACE(ページ, "[-1] 前ページ")% %RETURNSPACE(次キャラあり, "[-9] 次ページ")%"
ENDIF

@メインメニュー_キャラ一覧_INPUT


@メインメニュー_人員の補充
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 対象
#DIM DYNAMIC LINE
#DIMS 雇用キャラ名

CALL COLUMNPRINTL , 0, @"□所持資源:{資源}個"
FOR LCOUNT, 0, CHARANUM
	SIF !ステータス:LCOUNT:未加入
		CONTINUE
	対象 = LCOUNT
	CALL ステータス表示, 対象, @"[{LCOUNT}] - 雇う\@ ステータス:対象:レベル > 1 ? (資源{ステータス:対象:レベル}個必要）#\@"
	CALL COLUMNPRINTL, 0, ""
NEXT
IF 雇用キャラ
	IF CRESULT:1 == 0
		CALL COLUMNPRINTL, 0, @"%雇用キャラ名%を雇った"
		雇用キャラ = 0
	ELSE
		IF ステータス:雇用キャラ:レベル > 1 && 資源 < ステータス:雇用キャラ:レベル
			CALL COLUMNPRINTL, 0, @"%NAME:雇用キャラ%を雇うには資源が足りません"
		ELSE
			CALL COLUMNPRINTL, 0, @"%NAME:雇用キャラ%を雇いますか？"
			雇用キャラ名 = %NAME:雇用キャラ%
			CALL COLUMNPRINTL, 0, @"[0] はい [1] いいえ"
		ENDIF
	ENDIF
ENDIF

@メインメニュー_人員の補充_INPUT
IF 雇用キャラ
	IF CRESULT:1 == 0
		ステータス:雇用キャラ:未加入 = 0
		SORTCHARA ステータス:未加入
	ELSE
		雇用キャラ = 0
	ENDIF
ELSE
	雇用キャラ = CRESULT:1
ENDIF

@メインメニュー_アイテム
#DIM DYNAMIC LINE
#DIM DYNAMIC LCOUNT
#DIMS DYNAMIC 分類
#DIMS DYNAMIC 装備条件
#DIM DYNAMIC 要求値
#DIMS DYNAMIC 文字色
{
#DIMS DYNAMIC 要求項目 =
"必要筋力",
"必要耐久",
"必要知性",
"必要器用",
"必要感覚",
"必要運勢",
}

CALL COLUMNPRINTL ,0, @"□所持資源:{資源}個 倉庫:{拠点アイテム総数()}/{倉庫}"

FOR LCOUNT, アイテムページ*10, アイテムページ*10+10
	IF !拠点アイテム(LCOUNT)
		CALL COLUMNPRINTL, 0, ""
	ELSE
		SELECTCASE レア度(LCOUNT)
			CASE 1
				文字色 = silver
			CASE 2
				文字色 = lime
			CASE 3
				文字色 = deepskyblue
			CASE 4
				文字色 = purple
			CASE IS >= 5
				文字色 = gold
		ENDSELECT
		CALL COLUMNPRINTL, 0, @"[{LCOUNT}] %アイテム名(LCOUNT)%(レア度:{レア度(LCOUNT)})", 文字色
	ENDIF
NEXT
CALL COLUMNPRINTL, 0, @"%RETURNSPACE(アイテムページ > 0, "[201] 前ページ")% %RETURNSPACE(DT_ROW_LENGTH("アイテム") > LCOUNT && 拠点アイテム(LCOUNT+1), "[209] 次ページ")%"
IF CRESULT:1 == 210
	CALL COLUMNPRINTL, 0, @"　%売却アイテム名%を売却しました(資源{売却額}個分)"
ELSEIF CRESULT:1 >= 0 && アイテム名(CRESULT:1) != ""
	FOR LCOUNT, 0, VARSIZE("要求項目")
		要求値 = 要求ステータス(CRESULT:1, 要求項目:LCOUNT)
		SIF 要求値
			装備条件 += @"%要求項目:LCOUNT%:{要求値} "
	NEXT
	分類 = %DT_CELL_GETS("アイテム", CRESULT:1, "分類")%
	CALL COLUMNPRINTL, 0, @"□%DT_CELL_GETS("アイテム", CRESULT:1, "名前")%（ID:{CRESULT:1}）"
	CALL COLUMNPRINTL, 0, @"　レア度:{レア度(CRESULT:1)} \@ 分類 == "消耗品" ? 残り使用回数 # 耐久値 \@:{耐久値(CRESULT:1)} 分類:%分類%"
	SIF 装備条件 != ""
		CALL COLUMNPRINTL, 0, @"　%装備条件%"
	CALL COLUMNPRINTL, 0, @"　\@ 攻撃力(CRESULT:1) ? 攻撃力:{攻撃力(CRESULT:1)}%" "% #\@\@ 防御力(CRESULT:1) ? 防御力:{防御力(CRESULT:1)}%" "% #\@\@ 命中率(CRESULT:1) ? 命中率:{命中率(CRESULT:1)}%" "% #\@\@ 特殊効果(CRESULT:1) != "" ? 特殊効果:%特殊効果(CRESULT:1)%%" "% #\@\@ 重量(CRESULT:1) ? 重量:{重量(CRESULT:1)} #\@"
	CALL COLUMNPRINTL, 0, @"　%説明(CRESULT:1)%"
	CALL COLUMNPRINTL, 0, @"　[210] 売却する（資源{レア度(CRESULT:1)}個分）"
ENDIF

@メインメニュー_アイテム_INPUT
SELECTCASE CRESULT:1
	CASE 0 TO 99
		選択アイテム = CRESULT:1
	CASE 201
		SIF アイテムページ > 0
			アイテムページ--
	CASE 209
		SIF アイテムページ < 9
			アイテムページ++
	CASE 210
		IF レア度(選択アイテム)
			売却アイテム名 = %アイテム名(選択アイテム)%
			売却額 = レア度(選択アイテム)
			資源 += 売却額
			CALL アイテム削除(選択アイテム)
		ENDIF
ENDSELECT

@メインメニュー_セーブ＆ロード
#DIM DYNAMIC LINE
#DIM DYNAMIC LCOUNT

CALL COLUMNPRINTL, 0, "□セーブ"
CALL COLUMNPRINTL, 0, @"[10] セーブする（セーブデータID:{SAVEID}）"
CALL COLUMNPRINTL, 0, ""
CALL COLUMNPRINTL, 0, @"□ロード"
FOR LCOUNT, 0, 10
	SELECTCASE CHKDATA(LCOUNT)
		CASE 0
			CALL COLUMNPRINTL, 0, @"[{LCOUNT}] %RESULTS%"
		CASE 1
			CALL COLUMNPRINTL, 0, @"[{LCOUNT}] ----", "gray"
			RESETCOLOR
		CASE 2 TO 4
			CALL COLUMNPRINTL, 0, @"[{LCOUNT}] 無効なセーブデータ", "gray"
	ENDSELECT
NEXT
CALL COLUMNPRINTL, 0, ""
SIF CRESULT:1 == 10
	CALL COLUMNPRINTL, 0, "セーブしました"

@メインメニュー_セーブ＆ロード_INPUT
SELECTCASE CRESULT:1
	CASE 0 TO 9
		IF CHKDATA(CRESULT:1) == 0
			LOADDATA CRESULT:1
		ENDIF
	CASE 10
		SAVEDATA SAVEID, @"{SAVEID}"
ENDSELECT

@加入候補生成
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC 対象
#DIM DYNAMIC 乱数
DUMPRAND
乱数 = RANDDATA
RANDOMIZE シード値:キャラクター
FOR LCOUNT, 0, 5
	ADDVOIDCHARA
	対象 = CHARANUM-1
	NAME:対象 = %ランダム命名(RAND(1, 4))%
	;1から始める
	NO:対象 = ++NOCOUNT
	;3人目までが資源が必要 4～5人目は志願兵
	SELECTCASE LCOUNT
		CASE 0 TO 2
			ステータス:対象:レベル = RAND(6, 11)
		CASE 3, 4
			ステータス:対象:レベル = 1
	ENDSELECT
	CSTR:対象:職業 = %職業一覧:(RAND:VARSIZE("職業一覧"))%
	CALL 職業初期値, CSTR:対象:職業, 対象
	IF 職業顔グラ(CSTR:対象:職業) != ""
		CSTR:対象:顔グラ = %RESULTS:(RAND:ENUMFILES(@"resources/%職業顔グラ(CSTR:対象:職業)%"))%
		;文頭の「resources\」の11文字を消す
		CSTR:対象:顔グラ = %SUBSTRING(CSTR:対象:顔グラ, 10)%
	ENDIF
	FOR LCOUNT2, 1, ステータス:対象:レベル
		ステータス:対象:(RAND:6)++
	NEXT
	ステータス:対象:HP = MAXHP(対象)
	ステータス:対象:MP = MAXMP(対象)
	ステータス:対象:VP = MAXVP(対象)
	;ステータスの設定 保留
	ステータス:対象:未加入 = 1
NEXT

加入候補生成済み = 1

DUMPRAND
シード値:キャラクター = RANDDATA
RANDOMIZE 乱数
